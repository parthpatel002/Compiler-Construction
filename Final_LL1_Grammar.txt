<program> ===> <otherFunctions> <mainFunction>
<mainFunction>===> TK_MAIN <stmts> TK_END
<otherFunctions>===> <function><otherFunctions> | eps
<function>===>TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par>===>TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par>===>TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR | eps
<parameter_list>===><dataType> TK_ID <remaining_list>
<dataType>===> <primitiveDatatype> |<constructedDatatype>
<primitiveDatatype>===> TK_INT | TK_REAL
<constructedDatatype>===>TK_RECORD TK_RECORDID
<remaining_list>===>TK_COMMA <parameter_list> | eps
<stmts>===><typeDefinitions> <declarations> <otherStmts><returnStmt>
<typeDefinitions>===><typeDefinition><typeDefinitions> |eps
<typeDefinition>===>TK_RECORD TK_RECORDID <fieldDefinitions> TK_ENDRECORD TK_SEM
<fieldDefinitions>===> <fieldDefinition><fieldDefinition><moreFields>
fieldDefinition>===> TK_TYPE <primitiveDatatype> TK_COLON TK_FIELDID TK_SEM
<moreFields>===><fieldDefinition><moreFields> | eps
<declarations> ===> <declaration><declarations>|eps
<declaration>===> TK_TYPE <dataType> TK_COLON TK_ID <declaration'> TK_SEM
<declaration'>===>TK_COLON <global_or_not> | eps
<global_or_not>===>TK_GLOBAL
<otherStmts>===> <stmt><otherStmts> | eps
<stmt>===> <assignmentStmt> | <iterativeStmt>|<conditionalStmt>|<ioStmt>| <funCallStmt>
<assignmentStmt>===><SingleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId>===>TK_ID | TK_RECORDID TK_DOT TK_FIELDID
<funCallStmt>===><outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ==> TK_SQL <idList> TK_SQR TK_ASSIGNOP | eps
<inputParameters>===> TK_SQL <idList> TK_SQR
<iterativeStmt>===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt><otherStmts> TK_ENDWHILE
<conditionalStmt>===> TK_IF <booleanExpression> TK_THEN <stmt><otherStmts><conditionalStmt'>
<conditionalStmt'>===> TK_ELSE <otherStmts> TK_ENDIF | TK_ENDIF
<ioStmt>===>TK_READ TK_OP <allVar> TK_CL TK_SEM | TK_WRITE TK_OP <allVar> TK_CL TK_SEM
<allVar>===><var>| TK_RECORDID
<arithmeticExpression>===><term> <expression>
<expression>===> <operator1> <term> <expression> | eps
<term>===> <factor><term'>
<term'>===> <operator2> <factor> <term'> | eps
<factor>===> TK_OP <arithmeticExpression> TK_CL | <var>
<operator1> ===> TK_PLUS | TK_MINUS
<operator2> ===> TK_MUL | TK_DIV
<booleanExpression>===>TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
<booleanExpression>===> <var> <relationalOp> <var>
<booleanExpression>===> TK_NOT <booleanExpression>
<var>===> TK_ID | TK_NUM | TK_RNUM
<logicalOp>===>TK_AND | TK_OR
<relationalOp>===> TK_LT | TK_LE | TK_EQ |TK_GT | TK_GE | TK_NE
<returnStmt>===>TK_RETURN <optionalReturn> TK_SEM
<optionalReturn>===>TK_SQL <idList> TK_SQR | eps
<idList>===> TK_ID <more_ids>
<more_ids>===> TK_COMMA <idList> | eps
